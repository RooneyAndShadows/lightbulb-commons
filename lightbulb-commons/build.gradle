plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

android {
    compileSdkVersion 34
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionName "1.3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildFeatures {
        dataBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'com.github.rooneyandshadows.lightbulb.commons'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"
    options.deprecation = true
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        repositories {
            mavenLocal()
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId "$environment.group_id"
                version android.defaultConfig.versionName
                artifactId project.name
                artifact androidSourcesJar
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.core:core-ktx:$versions.ktx.core"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$versions.android.desugar"
    implementation "androidx.appcompat:appcompat:$versions.android.appcompat"
    implementation "androidx.biometric:biometric:$versions.android.biometric"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.android.lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.android.lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.android.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$versions.android.lifecycle"
    implementation "androidx.preference:preference-ktx:$versions.android.preference"
    implementation "com.github.rooneyandshadows:java-commons:$versions.java.commons"
    implementation "com.google.android.material:material:$versions.android.material"
    implementation "com.google.android.gms:play-services-auth:$versions.g_play_services_auth"
    api "com.mikepenz:iconics-core:5.3.0"
    api "com.mikepenz:fontawesome-typeface:5.9.0.2-kotlin@aar"
}